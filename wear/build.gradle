apply plugin: 'com.android.application'
apply plugin: 'android-apt'

def keystorePropertiesFile = rootProject.file("devoxx.properties");
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

def PACKAGE_NAME = "com.devoxx"
def VERSION_CODE = keystoreProperties['code'].toInteger()
def VERSION_NAME = keystoreProperties['name']

android {
    compileSdkVersion 22
    buildToolsVersion '25.0.2'

    publishNonDefault true

    defaultConfig {
        applicationId PACKAGE_NAME
        minSdkVersion 21
        targetSdkVersion 22
        versionCode VERSION_CODE
        versionName VERSION_NAME
    }

    signingConfigs {
        debug {
            def signFilePath = "${project(':app').projectDir}/debug.keystore"
            storeFile file(signFilePath)
        }
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            def signFilePath = "${project(':app').projectDir}/${keystoreProperties['storeFile']}"
            storeFile file(signFilePath)
            storePassword keystoreProperties['storePassword']
        }
    }

    productFlavors {
        withIntegration {
            buildConfigField 'boolean', 'INTEGRATION', "true"
        }
        noIntegration {
            buildConfigField 'boolean', 'INTEGRATION', "false"
        }
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled false
            signingConfig signingConfigs.release
        }
        debug {
            debuggable true
            signingConfig signingConfigs.debug
        }
    }
}

apt {
    arguments {
        androidManifestFile variant.outputs[0].processResources.manifestFile
        resourcePackageName PACKAGE_NAME
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.google.android.support:wearable:1.3.0'
    compile 'com.google.android.gms:play-services-wearable:8.4.0'


    apt 'org.androidannotations:androidannotations:3.3.2'
    compile 'org.androidannotations:androidannotations-api:3.3.2'

    // Event Bus: BusWear
    compile 'pl.tajchert:buswear:0.9.5'

    // TODO: update to 2.0
    // Circled image
    compile 'de.hdodenhof:circleimageview:1.3.0'

    // Org Apache commons lang
    compile 'org.apache.commons:commons-lang3:3.4'

    compile project(path: ':common')


}
