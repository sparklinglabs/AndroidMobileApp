buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.21.2'
        classpath 'me.tatarka:gradle-retrolambda:3.2.4'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'android-apt'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'realm-android'

def keystorePropertiesFile = rootProject.file("devoxx.properties");
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

def PACKAGE_NAME = "com.devoxx"
def VERSION_CODE = keystoreProperties['code'].toInteger()
def VERSION_NAME = keystoreProperties['name']

def mapKey = keystoreProperties['mapKey']

android {
    compileSdkVersion 23
    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId PACKAGE_NAME
        minSdkVersion 16
        targetSdkVersion 23
        versionCode VERSION_CODE
        versionName VERSION_NAME
        manifestPlaceholders = [applicationLabel: "@string/app_name", mapKey: mapKey]

        renderscriptTargetApi 18
        renderscriptSupportModeEnabled true
    }

    signingConfigs {
        debug {
            storeFile file("debug.keystore")
        }
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file("${projectDir}/${keystoreProperties['storeFile']}")
            storePassword keystoreProperties['storePassword']
        }
    }

    flavorDimensions("test_vote", "integration")

    productFlavors {
        withIntegration {
            dimension = "integration"
            buildConfigField 'boolean', 'INTEGRATION', "true"
        }
        noIntegration {
            dimension = "integration"
            buildConfigField 'boolean', 'INTEGRATION', "false"
        }
        vote_sandbox_enabled {
            dimension = "test_vote"
            buildConfigField 'boolean', 'TEST_VOTE', "true"
        }
        vote_sandbox_disabled {
            dimension = "test_vote"
            buildConfigField 'boolean', 'TEST_VOTE', "false"
        }
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled false
            manifestPlaceholders = [applicationLabel: "@string/app_name", mapKey: mapKey]
            signingConfig signingConfigs.release
        }
        debug {
            debuggable true
            signingConfig signingConfigs.debug
            manifestPlaceholders = [applicationLabel: "@string/app_name", mapKey: mapKey]
        }
    }
    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

apt {
    arguments {
        androidManifestFile variant.outputs[0].processResources.manifestFile
        resourcePackageName PACKAGE_NAME
    }
}

def SUPPORT_LIBRARY_VERSION = "23.2.1"

dependencies {
    repositories {
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url 'https://maven.fabric.io/public' }
        mavenCentral()
    }
    compile fileTree(include: ['*.jar'], dir: 'libs')

    // Android Wear support
    withIntegrationWearApp project(path: ':wear', configuration: 'withIntegrationRelease')
    noIntegrationWearApp project(path: ':wear', configuration: 'noIntegrationRelease')

    compile "com.android.support:appcompat-v7:${SUPPORT_LIBRARY_VERSION}"
    compile "com.android.support:design:${SUPPORT_LIBRARY_VERSION}"
    compile "com.android.support:support-v4:${SUPPORT_LIBRARY_VERSION}"

    compile 'com.google.android.gms:play-services-maps:8.4.0'
    compile 'com.google.android.gms:play-services-vision:8.4.0'
    compile 'com.google.android.gms:play-services-wearable:8.4.0'
    compile 'com.google.android.gms:play-services-ads:8.4.0'

    compile 'org.jetbrains:annotations:13.0'
    compile 'com.makeramen:roundedimageview:2.2.0'

    compile 'com.squareup.okhttp3:okhttp:3.2.0'
    compile('com.squareup.retrofit2:retrofit:2.0.0') {
        exclude module: 'okhttp'
    }
    compile 'com.squareup.retrofit2:converter-gson:2.0.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.0.1'
    compile 'com.google.code.gson:gson:2.4'

    compile 'com.annimon:stream:1.0.8'

    compile 'net.danlew:android.joda:2.9.0'

    apt 'org.androidannotations:androidannotations:4.0.0'
    compile 'org.androidannotations:androidannotations-api:4.0.0'

    compile 'com.github.bumptech.glide:glide:3.6.1'

    compile('com.github.afollestad.material-dialogs:core:0.8.5.3@aar') {
        transitive = true
    }

    compile('com.crashlytics.sdk.android:crashlytics:2.5.3@aar') {
        transitive = true
    }

    compile 'com.commit451:PhotoView:1.2.4'
    compile 'com.google.android:flexbox:0.1.3'

    // Event Bus: BusWear
    compile 'pl.tajchert:buswear:0.9.5'

    compile project(path: ':scanner')

    compile project(path: ':common')

    compile project(path: ':huntly_integration')

}

def JAVA8_HOME_PATH = keystoreProperties['java8_path']

retrolambda {
    jdk JAVA8_HOME_PATH
    javaVersion JavaVersion.VERSION_1_7
    defaultMethods false
    incremental true
}
